:Namespace Y
⎕IO←0  ⍝ ⎕IO delenda est
I←⌷⍨∘⊂

HT LF CR←⎕UCS 9 10 13
M S K←1+⍳3
TM TS TK←1+⍳3

PS←{
 1<≢⍴⍵: 'PARSER EXPECTS VECTOR INPUT'            ⎕SIGNAL 11
 2<|≡⍵: 'PARSER EXPECTS DEPTH OF CHARS OR LINES' ⎕SIGNAL 11
 ∨/0≠10|⎕DR¨⊆⍵: 'PARSER EXPECTS CHAR INPUT'      ⎕SIGNAL 11

 c←¯1↓¨(¯1⌽LF=z)⊂z←∊LF,⍨⍪⊆⍵
 k t←(≢c)∘⍴¨K TK

 d a q←⊂(≢c)⍴0

 d k t a q c/⍨←⊂(⊂'')≢¨c

 tt←'tag:yaml.org,2002:'∘,¨'map' 'seq' 'str'
 qq←,⊂''
 ver←1 2
 hnd←(,'!')'!!'
 pfx←(,'!')'tag:yaml.org,2002:'

 (d k t a q c)(tt qq ver hnd pfx)}

∇{z}←TEST;TAP;ERRS
 z←0  ⍝ Profiling with cmpx requires result
 TAP←{⍺←⊢ ⋄ 0∊⍵: ⍺ ⎕SIGNAL 8 ⋄ ⎕←'ok - ',(⎕CR'#.Y.TEST')[1⊃⎕LC;] ⋄ _←0}
 ERRS←{⍺←⊢ ⋄ 0::⎕EN ⋄ 0⊣⍺ ⍺⍺ ⍵}

⍝ Empty input
 (d k t a q c)(tt qq ver hnd pfx)←PS ''
 TAP 0=≢¨d k t a q c
 TAP 1=≢∘⍴¨d k t a q c
 TAP 0=⊃¨d k t a q
 TAP c≡0⍴⊂''
 TAP tt≡'tag:yaml.org,2002:'∘,¨'map' 'seq' 'str'
 TAP qq≡,⊂''
 TAP ver≡1 2
 TAP hnd≡(,'!')'!!'
 TAP pfx≡(,'!')'tag:yaml.org,2002:'

⍝ Invalid input
 TAP 11=PS ERRS ↑'fire' 'flies' 'at' 'dusk'
 TAP 11=PS ERRS ⍳5
 TAP 11=PS ERRS 'dally' 10 'monday'

⍝ Plain scalar singleton
 (d k t _ _ c)_←PS 'beware the drool'
 TAP d=0
 TAP k=K
 TAP t=TK
 TAP c≡,⊂'beware the drool'
∇
:EndNamespace
