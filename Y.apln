:Namespace Y
⎕IO←0  ⍝ ⎕IO delenda est
I←⌷⍨∘⊂

HT LF CR←⎕UCS 9 10 13
M S K←1+⍳3
TM TS TK←1+⍳3

PS←{
 1<≢⍴⍵: 'PARSER EXPECTS VECTOR INPUT'            ⎕SIGNAL 11
 2<|≡⍵: 'PARSER EXPECTS DEPTH OF CHARS OR LINES' ⎕SIGNAL 11
 ∨/0≠10|⎕DR¨⊆⍵: 'PARSER EXPECTS CHAR INPUT'      ⎕SIGNAL 11

 c←¯1↓¨(¯1⌽LF=z)⊂z←∊LF,⍨⍪⊆⍵

 c←(1↓⊢)∘{⍵↑⍨1⍳⍨('#'=⍵)∧¯1⌽' '=⍵}∘(' ',⊢)¨c

 c←{⍵↓⍨1⍳⍨~⍵∊' 'HT}¨c
 c←⌽∘{⍵↓⍨1⍳⍨~⍵∊' 'HT}∘⌽¨c

 c/⍨←(⊂'')≢¨c

 m←∊(c≡¨⊂'''''')∨(1∘↑∧¯1∘↑)∘(2↓⊢)∘(⊢>0,2⊣/⊢)∘(0 0,⊢)∘(''''=⊢)¨c
 (m/c)←(1↓¯1↓⊢)¨m/c

 m←∊(1∘↑∧¯1∘↑)∘('"'=⊢)¨c
 (m/c)←(1↓¯1↓⊢)¨m/c

 d a q←⊂(≢c)⍴0 ⋄ k t←(≢c)∘⍴¨K TK

 tt←'tag:yaml.org,2002:'∘,¨'map' 'seq' 'str'
 qq←,⊂''
 ver←1 2
 hnd←(,'!')'!!'
 pfx←(,'!')'tag:yaml.org,2002:'

 (d k t a q c)(tt qq ver hnd pfx)}

∇{z}←TEST;TAP;ERRS;d;k;t;a;q;c;tt;qq;ver;hnd;pfx
 z←0  ⍝ Profiling with cmpx requires result
 TAP←{⍺←⊢ ⋄ 0∊⍵: ⍺ ⎕SIGNAL 8 ⋄ ⎕←'ok - ',4↓(⎕CR'#.Y.TEST')[1⊃⎕LC;] ⋄ _←0}
 ERRS←{⍺←⊢ ⋄ 0::⎕EN ⋄ 0⊣⍺ ⍺⍺ ⍵}

⎕←'# Empty input'
 (d k t a q c)(tt qq ver hnd pfx)←PS ''
 TAP 0=≢¨d k t a q c
 TAP 1=≢∘⍴¨d k t a q c
 TAP 0=⊃¨d k t a q
 TAP c≡0⍴⊂''
 TAP tt≡'tag:yaml.org,2002:'∘,¨'map' 'seq' 'str'
 TAP qq≡,⊂''
 TAP ver≡1 2
 TAP hnd≡(,'!')'!!'
 TAP pfx≡(,'!')'tag:yaml.org,2002:'

⎕←'# Invalid input'
 TAP 11=PS ERRS ↑'fire' 'flies' 'at' 'dusk'
 TAP 11=PS ERRS ⍳5
 TAP 11=PS ERRS 'dally' 10 'monday'

⎕←'# Plain scalar singleton'
 (d k t _ _ c)_←PS 'beware the drool'
 TAP d=0
 TAP k=K
 TAP t=TK
 TAP c≡,⊂'beware the drool'

⎕←'# One-line comment singleton'
 (d _ _ _ _ _)_←PS '# asparagus goodies'
 TAP d≡⍬

⎕←'# Plain scalar with whitespace padding'
 (_ _ _ _ _ c)_←PS '   drosophilia melancholia     '
 TAP c≡,⊂'drosophilia melancholia'

⎕←'# Inline comment'
 (_ _ _ _ _ c)_←PS'monkey business # for profit'
 TAP c≡,⊂'monkey business'

⎕←'# Single-quoted string'
 (_ k t _ _ c)_←PS '''fiona barrusta'''
 TAP k=K
 TAP t=TK
 TAP c≡,⊂'fiona barrusta'

⎕←'# Single-quoted empty scalar'
 (_ k t _ _ c)_←PS ''''''
 TAP k=K
 TAP t=TK
 TAP c=,⊂''

⎕←'# Double-quoted scalar'
 (_ k t _ _ c)_←PS '"dreamers in droves"'
 TAP k=K
 TAP t=TK
 TAP c≡,⊂'dreamers in droves'

⎕←'# Double-quoted empty scalar'
 (_ k t _ _ c)_←PS '""'
 TAP k=K
 TAP t=TK
 TAP c≡,⊂''
∇
:EndNamespace
