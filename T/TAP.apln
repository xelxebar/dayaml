:Namespace TAP
∆SUCC ∆FAIL ∆SKIP ∆ID ∆SUITE ∆DESC ∆DIR ∆DIAG←(5⍴⊂⍬),(2⍴⊂''),⊂0⍴⊂''

OK←{∆SUCC∘←1+⊃∆SUCC ⋄ ⍵ LINE 'ok'}
NOTOK←{∆FAIL∘←(⊃∆FAIL)+'# TODO'≢6↑∆DIR ⋄ ⍵ LINE 'notok'}
BAIL←{∆SUITE∘←(⍴∆SUITE)⍴0
 ⎕←∊' ',⍨⍪'Bail out!' ⍵
 CLEAN 0}
SKIP←{⍺←0 ⋄ ∆SKIP∘←1+⊃∆SKIP ⋄ ∆DIR∘←'# SKIP ',⍵
 ⍺ LINE 'ok'}

TODO←{∆DIR∘←'# TODO ',⍵}
DESC←{∆DESC∘←⍵}
DIAG←{∆DIAG∘←∆DIAG,⊂'# ',⍵}

LINE←{∆ID∘←1+⊃∆ID
 ⎕←∊' ',⍨⍪⍵ ∆ID ∆DESC ∆DIR
 ⍞←∊(⎕UCS 13),⍨↑∆DIAG
 CLEAN ⍺}

SUITE←{∆SUITE∘←1 ⋄ ∆SUCC ∆FAIL ∆SKIP ∆ID∘←0
 f←(⍵⎕S'&')↓##.⎕NL 3
 ⎕←'1..',⍕≢f
 _←{⍎'##.',⍵,'⋄ 0'}¨f
 n←'Failed' 'Succeeded' 'Skipped' 'Total'
 ⎕←n,⍪∆FAIL ∆SUCC ∆SKIP (≢f)
 ∆SUCC ∆FAIL ∆SKIP ∆ID ∆SUITE∘←5⍴⊂⍬}

CLEAN←{∆DESC ∆DIR ∆DIAG∘←(2⍴⊂''),⊂0⍴⊂'' ⋄ ⍵}

:EndNamespace

⍝ OK    ()   ∆SUCC←                        ∆SUITE
⍝ NOTOK ()           ∆FAIL←                ∆SUITE           ∆DIR
⍝ BAIL  (m)                                ∆SUITE←
⍝ SKIP  (m)                  ∆SKIP←        ∆SUITE←          ∆DIR←
⍝
⍝ TODO  (m)                                                 ∆DIR←
⍝ DESC  (d)                                         ∆DESC←
⍝ DIAG  (m)                                                        ∆DIAG←
⍝
⍝ LINE  (r)                          ∆ID   ∆SUITE→  ∆DESC*  ∆DIR*  ∆DIAG*
⍝ SUITE (r)  ∆SUCC*  ∆FAIL*          ∆ID   ∆SUITE*
⍝ CLEAN ()   ∆SUCC*  ∆FAIL*  ∆SKIP*  ∆ID*           ∆DESC*  ∆DIR*  ∆DIAG*
