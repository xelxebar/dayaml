T0000∆STRINGS_COMMENTS;DESC;IN;TEST;TRAP;d;k;t;a;i;c;s;e;tt;ii;v;h;p
 ⍝ ast meta←(d k t a i c s e) (tt ii v h p)
LF←⎕UCS 10

'Empty document' WITH ''

'Single line plain string' WITH 'agent moss'
  d k t a i c s e⍪←0 Y.P Y.T_NSP 0 0 (⊂IN) 0 (≢IN)

'Invalid char' WITH 'roger',⎕UCS 0
  TRAP←{msg←'INVALID CHARACTER AT POSITION(S) ',⍕¯1+≢IN
   R⊣TAP.DIAG ('Unexpected message: ',msg)/⍨~R←⎕DMX.EM≡msg}
  EXEC←{_←Y.PS IN ⋄ 0⊣TAP.DIAG 'Missed invalid character'}

'Singleton single-quoted string' WITH '''magnificent oil'''
  d k t a i c s e⍪←0 Y.Q Y.T_STR 0 0 (⊂IN~'''')0(≢IN)

'Singleton double-quoted string' WITH '"abilene"'
  d k t a i c s e⍪←0 Y.D Y.T_STR 0 0 (⊂IN~'"')0(≢IN)

'Quoted string with padding' WITH '   "',str,'"  '⊣str←'trees are sentient'
  d k t a i c s e⍪←0 Y.D Y.T_STR 0 0 (⊂str)0(≢IN)

'Escaped double quote in QQ string' WITH '"mains \"power\""'
  d k t a i c s e⍪←0 Y.D Y.T_STR 0 0 (⊂'mains "power"')0(≢IN)

'Sequence node' WITH '- ',str←'druids in the forest'
  d k t a i c s e⍪←(0 1)(Y.S Y.P)(Y.T_SEQ Y.T_NSP)(0 0)(0 0)('' str)(0 0)(2⍴≢IN)

'Multi-node sequence' WITH ∊('- ',LF,⍨⊢)¨stra strb←'multimillion acres' 'dry koolaid'
  d k t a i⍪←(0 1 1)(Y.S Y.P Y.P)(Y.T_SEQ Y.T_NSP Y.T_NSP)(0 0 0)(0 0 0)
  c s e⍪←('' stra strb)(0,⍸¯1⌽m)((⊃∘⌽,⊢)⍸m←LF=IN)

'Explicit-key map' WITH ∊,∘LF¨('? "','"',⍨key←'cute bunny')(': ',val←'Angkor Wat')
  d k t a i c s e⍪←(0 1)(Y.M Y.P)(Y.T_MAP Y.T_NSP)(0 0)(0 1)('' val)(0 0)(2⍴¯1+≢IN)
  ii⍪←,⊂key

'Multi-value explicit-key map' WITH ∊LF,⍨'?:?:',' ',↑(keya←'mip')(vala←'rip')(keyb←'lip')(valb←'zip')
  d k t a i⍪←Y.((0 1 1)(M P P)(T_MAP T_NSP T_NSP)(0 0 0)(0 1 2))
  c s e ii⍪←('' vala valb)(0,⍸'?'=IN)((¯1+≢IN),⍸1⌽'?'=IN)(keya keyb)

SUITE_END⍬
