 P←{IN←⍵ ⋄ M S X←1+⍳3
I←{(⊂⍵)⌷⍺} ⋄ U←{⍺←⊢ ⋄ ⍺ ⍵⍵⍣¯1⍤⍺⍺⍥⍵⍵ ⍵} ⋄ R←{⍺⍺⍨/U⌽¨,\⍵}
X←16∘⊥∘((⎕D,'ABCDEF')∘⍳)¨
assert←{⍺←'assertion failure' ⋄ 0∊⍵:⍎'⍺ ⎕SIGNAL 8' ⋄ shy←0}

1<≢⍴IN:'PARSER EXPECTS VECTOR INPUT'⎕SIGNAL 11
1<|≡IN:'PARSER EXPECTS SIMPLE VECTOR INPUT'⎕SIGNAL 11
0≠10|⎕DR IN:'PARSER EXPECTS CHARACTER VECTOR'⎕SIGNAL 11

HT CR LF←⎕UCS 09 10 13
IN,←LF/⍨LF≠¯1↑IN←LF@(=∘CR)IN/⍨ ~CR LF⍷IN

s←0,(⍸¯1=+⍀⍣¯1⊢1,⍨IN∊LF) ⋄ e←(¯1↓1⌽s),≢IN ⋄ INN←(LF∘≠⊆⊢)IN
 SIGNAL←{⍺←11 'PARSER ERROR' ⋄ EN EM∘←⍺ ⋄ lines←∪s⍸⍵
nos⍀⍨←1 0⍴⍨2×≢nos←'[',(⍕⍪lines),⍤1⊢'] '
msg←⍪~∘LF¨IN∘I¨idx←beg+⍳¨e[lines]-beg←s[lines]
msg←¯1↓∊LF,⍨nos,⍪,msg,' ‾'∘I¨∊∘⍵¨idx
dmx←('EN' EN)('EM' EM)('Message'(EM,LF,msg))
dmx,←('Category' 'Parser')('Vendor' 'dayaml')
⎕SIGNAL⊂dmx}

c_printable←X¨('20' '7E') ('A0' 'D7FF') ('E000' 'FFFD') ('010000' '10FFFF')
c_printable←⎕UCS∊(X'09' '0A' '0D' '85'),{⍺+⍳1+⍵-⍺}/¨c_printable
ns_char←c_printable~CR LF,HT ' ',X 'FEFF'
c_escapes ←∊       '0'  'a'  'b'  't'   HT  'n'  'v'  'f'  'r'  'e'
e_val     ←⎕UCS X '00' '07' '08' '09' '09' '0A' '0B' '0C' '0D' '1B'
c_escapes,←∊       ' '  '"'  '/'  '\'  'N'  '_'    'L'    'P'  'x'  'u'  'U'
e_val    ,←⎕UCS X '20' '22' '2F' '5C' '85' 'A0' '2028' '2029'
c_indicator←'-?:,[]{}#&*!|>''"%@`'
∨/m←~IN∊c_printable:11 ('INVALID CHARACTER AT POSITION(S) ',⍕n)SIGNAL⊢n←⍸m

tt←,¨'?!' ⋄ aa←,⊂''

state∆table ←⍉3 6⍴5 0 0 0 0 0  1 5 2 1 1 1  1 1 1 1 1 1
state∆table,←⍉3 6⍴3 3 3 3 4 3  0 1 2 3 4 5  0 1 2 3 4 5
state←{⌷∘state∆table⍤,/U⌽¨,\'''' '"' '\' '#' LF⍳⍵}IN
m←¯1⌽4=state ⋄ s←⍸m ⋄ e←⍸1⌽m ⋄ c←¯1↓¨m⊂IN ⋄ s+←c↓¨⍨←d←(1⍳⍨≠∘' ')¨c
s d+←⊂(1+1⍳⍨' '≠1↓¨⊢)U(m∘/)c⊣m←⊃¨'-'=c ⋄ d←¯1+≢∘∊¨{∪⍺[⍳1+⍺⍸⍵],⍵}⍥,⍨⌿U⌽¨,⍀d
⍝ '-?:'⍳⊃¨c

k i t a←⊂0⍴⍨≢d ⋄ ii←(≢d)⍴⊂''
d k t tt a aa i ii c s e}
