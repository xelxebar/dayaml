 P←{IN←⍵ ⋄ M S X←1+⍳3
I←{(⊂⍵)⌷⍺} ⋄ U←{⍺←⊢ ⋄ ⍺ ⍵⍵⍣¯1⍤⍺⍺⍥⍵⍵ ⍵} ⋄ R←{⍺⍺⍨/U⌽¨,\⍵}
X←16∘⊥∘((⎕D,'ABCDEF')∘⍳)¨
assert←{⍺←'assertion failure' ⋄ 0∊⍵:⍎'⍺ ⎕SIGNAL 8' ⋄ shy←0}

1<≢⍴IN:'PARSER EXPECTS VECTOR INPUT'⎕SIGNAL 11
1<|≡IN:'PARSER EXPECTS SIMPLE VECTOR INPUT'⎕SIGNAL 11
0≠10|⎕DR IN:'PARSER EXPECTS CHARACTER VECTOR'⎕SIGNAL 11

HT LF CR←⎕UCS 09 10 13
IN,←LF/⍨LF≠¯1↑IN←LF@(=∘CR)IN/⍨ ~CR LF⍷IN

s←0,(⍸¯1=+⍀⍣¯1⊢1,⍨IN∊LF) ⋄ e←(¯1↓1⌽s),≢IN ⋄ INN←(LF∘≠⊆⊢)IN
 SIGNAL←{⍺←11 'PARSER ERROR' ⋄ EN EM∘←⍺ ⋄ lines←∪s⍸⍵
nos⍀⍨←1 0⍴⍨2×≢nos←'[',(⍕⍪lines),⍤1⊢'] '
msg←⍪~∘LF¨IN∘I¨idx←beg+⍳¨e[lines]-beg←s[lines]
msg←¯1↓∊LF,⍨nos,⍪,msg,' ‾'∘I¨∊∘⍵¨idx
dmx←('EN' EN)('EM' EM)('Message'(EM,LF,msg))
dmx,←('Category' 'Parser')('Vendor' 'dayaml')
⎕SIGNAL⊂dmx}

c_printable←X¨('20' '7E') ('A0' 'D7FF') ('E000' 'FFFD') ('010000' '10FFFF')
c_printable←⎕UCS∊(X'09' '0A' '0D' '85'),{⍺+⍳1+⍵-⍺}/¨c_printable
ns_char←c_printable~CR LF,HT ' ',X 'FEFF'
c_escapes ←∊       '0'  'a'  'b'  't'   HT  'n'  'v'  'f'  'r'  'e'
e_val     ←⎕UCS X '00' '07' '08' '09' '09' '0A' '0B' '0C' '0D' '1B'
c_escapes,←∊       ' '  '"'  '/'  '\'  'N'  '_'    'L'    'P'  'x'  'u'  'U'
e_val    ,←⎕UCS X '20' '22' '2F' '5C' '85' 'A0' '2028' '2029'
c_indicator←'-?:,[]{}#&*!|>''"%@`'
∨/m←~IN∊c_printable:11 ('INVALID CHARACTER AT POSITION(S) ',⍕n)SIGNAL⊢n←⍸m

(d k t a i c s e) (tt ii v h p)←MTAST

s e←0 1∘.(⍸⍤⌽)⊂m←¯1⌽LF=IN
c←¯1∘↓¨m⊂IN
d←⍳∘1¨' '≠c
k←K_SCL⍴⍨≢d
t←T_STR⍴⍨≢d
s e←(1∘+@x⊢s) (¯1∘+@x⊢e)⊣x←⍸(=∧2≠⊢)/'''"'⍳IN[s,⍪s⌈¯1+e]
c←(¯1↓1↓⊢)¨@x⊢c
d k t s e c←(s≠e)∘/¨d k t s e c

(d k t a i c s e) (tt ii v h p)}

⍝ d - Depth
⍝ k - Kind
⍝ t - Tag, tt - Tag symbols
⍝ a - Alias, aa - Alias symbols
⍝ i - Index, ii - Index symbols
⍝ c - Content
⍝ s - Start
⍝ e - End
⍝ v - YAML Version
⍝ h - TAG Handles
⍝ p - TAG Prefixes
