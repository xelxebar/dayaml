 PS←{IN←⍵
I←{(⊂⍵)⌷⍺} ⋄ U←{⍺←⊢ ⋄ ⍺ ⍵⍵⍣¯1⍤⍺⍺⍥⍵⍵ ⍵} ⋄ R←{⍺⍺⍨/U⌽¨,\⍵}
X←16∘⊥∘((⎕D,'ABCDEF')∘⍳)¨
assert←{⍺←'assertion failure' ⋄ 0∊⍵:⍎'⍺ ⎕SIGNAL 8' ⋄ _←0}

1<≢⍴IN:'PARSER EXPECTS VECTOR INPUT'⎕SIGNAL 11
1<|≡IN:'PARSER EXPECTS SIMPLE VECTOR INPUT'⎕SIGNAL 11
0≠10|⎕DR IN:'PARSER EXPECTS CHARACTER VECTOR'⎕SIGNAL 11

HT LF CR←⎕UCS 09 10 13
IN,←LF/⍨LF≠¯1↑IN←LF@(=∘CR)IN/⍨ ~CR LF⍷IN

s←0,(⍸¯1=+⍀⍣¯1⊢1,⍨IN∊LF) ⋄ e←(¯1↓1⌽s),≢IN ⋄ INN←(LF∘≠⊆⊢)IN
 SIGNAL←{⍺←11 'PARSER ERROR' ⋄ EN EM∘←⍺ ⋄ lines←∪s⍸⍵
nos⍀⍨←1 0⍴⍨2×≢nos←'[',(⍕⍪lines),⍤1⊢'] '
msg←⍪~∘LF¨IN∘I¨idx←beg+⍳¨e[lines]-beg←s[lines]
msg←¯1↓∊LF,⍨nos,⍪,msg,' ‾'∘I¨∊∘⍵¨idx
dmx←('EN' EN)('EM' EM)('Message'(EM,LF,msg))
dmx,←('Category' 'Parser')('Vendor' 'dayaml')
⎕SIGNAL⊂dmx}

c_printable←X¨('20' '7E') ('A0' 'D7FF') ('E000' 'FFFD') ('010000' '10FFFF')
c_printable←⎕UCS∊(X'09' '0A' '0D' '85'),{⍺+⍳1+⍵-⍺}/¨c_printable
s_char←CR LF,HT ' ',X 'FEFF'
c_escapes ←∊       '0'  'a'  'b'  't'   HT  'n'  'v'  'f'  'r'  'e'
e_val     ←⎕UCS X '00' '07' '08' '09' '09' '0A' '0B' '0C' '0D' '1B'
c_escapes,←∊       ' '  '"'  '/'  '\'  'N'  '_'    'L'    'P'  'x'  'u'  'U'
e_val    ,←⎕UCS X '20' '22' '2F' '5C' '85' 'A0' '2028' '2029'
c_indicator←'-?:,[]{}#&*!|>''"%@`'
∨/m←~IN∊c_printable:11 ('INVALID CHARACTER AT POSITION(S) ',⍕n)SIGNAL⊢n←⍸m

(d k t a i c s e) (tt ii v h p)←MTAST

s e←0 1∘.(⍸⍤⌽)⊂m←¯1⌽LF=IN ⋄ c←¯1∘↓¨m⊂IN
d k t a i←⊂0⍴⍨≢⊃c s e←(s≠e)∘/¨c s e

c↓⍨¨←d←+/∘(∧\' '=⊢)¨c
d←¯1+≢∘∊¨{∪⍺[⍳⊃1+⍺⍸⍵],⍵}⍥,⍨/∘⌽¨,\d  ⍝ BURNINATE

g←S K V 0I'- ' '? ' ': '⍳2↑¨c
c(⊣@(⍸m)⍨)←2↓¨c/⍨m←g≠0

c/⍨¨←(∨\∧⌽∘(∨\)∘⌽)∘(~⍤∊∘' 'HT)¨c

(m/k)←(Q D P)I'''"'⍳∊1∘↑¨c/⍨m←0=k
(m/t)←(T_NSP T_STR T_STR)I(P Q D)⍳m/k
c[j]←(¯1↓1↓⊢)¨c[j←⍸k∊Q D]
c[⍸m]←{⍵/⍨~'\"'⍷⍵}¨c/⍨m←k=D

i[⍸g=V]←ii⍳ii∪←c[⍸g=K] ⋄ s[⍸g=V]←s[⍸g=K]
d k g t a i c s e/⍨←⊂g≠K

d k g t a i c s e m\⍨←⊂1+2</0,m←g∊S K V
m←d+←2∧/0,m ⋄ n←2</0,m
k t(⊣@(¯1+⍸n)⍨)←(n/S M M 0I⊢S K V⍳g)(n/T_SEQ T_MAP T_MAP 0I⊢S K V⍳g)
a i c s e(⊣@(¯1+⍸n)⍨)←0 0(⊂'')(s/⍨n)(e/⍨2>/0,⍨m)

(d k t a i c s e) (tt ii v h p)}

⍝ d - Depth
⍝ k - Kind
⍝ t - Tag, tt - Tag symbols
⍝ a - Alias, aa - Alias symbols
⍝ i - Index, ii - Index symbols
⍝ c - Content
⍝ s - Start
⍝ e - End
⍝ v - YAML Version
⍝ h - TAG Handles
⍝ p - TAG Prefixes
